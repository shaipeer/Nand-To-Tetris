// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t]==1) out[t+1] = 0
 * else if (load[t]==1)  out[t+1] = in[t]
 * else if (inc[t]==1)   out[t+1] = out[t] + 1  (integer addition)
 * else                  out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
	
	//if (reset[t]==1) out[t+1] = 0
    Mux16(a = in, b = false, sel = reset, out = resetMuxOut);

	//else if (load[t]==1)  out[t+1] = in[t]
    Or(a = load, b = reset, out = loadOrOut);
    Mux16(a = loopOut, b = resetMuxOut, sel = loadOrOut, out = loadMuxOut);

	//else if (inc[t]==1)   out[t+1] = out[t] + 1
    Or(a = loadOrOut, b = inc, out = incOrOut);
    Register(in = loadMuxOut, load = incOrOut, out = incRegisterOut, out = out);

	//else out[t+1] = out[t]
    Inc16(in = incRegisterOut, out = inc16Out);
    Mux16(a  = incRegisterOut, b = inc16Out, sel = inc, out = loopOut);
}

